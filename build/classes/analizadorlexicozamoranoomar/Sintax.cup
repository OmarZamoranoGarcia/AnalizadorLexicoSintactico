package analizadorlexicozamoranoomar;
import java_cup.runtime.*;

parser code 
{:
    public void syntax_error(Symbol s){       
        System.out.println("Error Sintáctico en la Línea " + (s.right) +
        " Columna "+s.left+ ". No se esperaba este componente: " +s.value+"."); 
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{         
        System.out.println("Error síntactico en la Línea " + 
        (s.right)+ " Columna "+s.left+". Componente " + s.value + 
        " no reconocido."); 
    }   
:} 

terminal String Identificador, Coma, Imprime, Parentesis_Izq, Parentesis_Der, PuntoComa, Numero, Literal, Error, Modificador_de_acceso, Tipo_de_dato, Devuelve_valor, Asigna, Llave_Der, Llave_Izq, Principal, Declarador_de_metodos, Corchete_Der, Corchete_Izq, Crear_clase, Ciclo_Mientras, Operador_comparativo, Importar, Agregador, Si, Sino, Sino_pregunta, Ciclo_Para, Incremento, Decremento, Punto, Constructor, Generador_de_objetos, Ingresa, Abridor_de_archivos;  // Tokens a usar
//no terminales a usar
non terminal MILENGUAJE,
Librerias, Libreria,
Clases, Clase, ConstructorClase, ParametroConstructorClase, ParametroConstructorClaseMas,
Metodos, Metodo, ParametroMetodo, ParametroMetodoMas, Retorno,
MetodoPrincipal, ListaInstruccionesMain, InstruccionesMain,
ListaInstSencillas, Instrucciones,
InstCrearObjeto, ParametrosObjeto, MasParametrosObjeto,
InstVariable, VariableVacia, IdNuLi,
InstPrint, ParametroPrint, MasParametrosPrint,
InstArreglo, ParametroArreglo, MasParametrosArreglo,
InstWhile, Condicion, NuId,
InstrAddArreglo,
InstIf, IfSino, SinoIf,
InstFor, SubeBaja, IncDec,
InstLlamarMetodo,
InstInput,
InstArbirArchivo ;


start with MILENGUAJE; //simbolo inicial

MILENGUAJE ::= Librerias Clases Metodos MetodoPrincipal; //estructura del lenguaje

//estructura para las librerias
Librerias ::= Libreria Librerias | /* vacío */; 
Libreria ::= Importar Identificador PuntoComa;

//estructura para las clases
Clases ::= Clase Clases | /* vacío */; 
Clase ::= Crear_clase Modificador_de_acceso Identificador Parentesis_Izq Parentesis_Der Llave_Izq ConstructorClase ListaInstSencillas Metodos Llave_Der ;
ConstructorClase ::= Constructor Parentesis_Izq ParametroConstructorClase Parentesis_Der | /* vacío */ ;
ParametroConstructorClase ::= Tipo_de_dato Identificador ParametroConstructorClaseMas ;
ParametroConstructorClaseMas ::= Coma ParametroConstructorClase  | /* vacío */ ;

//estructura para los metodos
Metodos ::= Metodo Metodos | /* vacío */; 
Metodo ::= Modificador_de_acceso Declarador_de_metodos Identificador Parentesis_Izq ParametroMetodo Parentesis_Der Llave_Izq ListaInstSencillas Retorno Llave_Der ;
ParametroMetodo ::= Tipo_de_dato Identificador ParametroMetodoMas | /* vacío */ ;
ParametroMetodoMas ::= Coma ParametroMetodo | /* vacío */ ;
Retorno ::= Devuelve_valor IdNuLi PuntoComa ;

//estructura para el metodo principal
MetodoPrincipal ::= Principal Parentesis_Izq Parentesis_Der Llave_Izq ListaInstruccionesMain Llave_Der ; 
ListaInstruccionesMain ::= InstruccionesMain ListaInstruccionesMain | /* vacío */ ;
InstruccionesMain ::= Instrucciones | InstCrearObjeto ;

//instruccion para crear un objeto solo dentro del main
InstCrearObjeto ::= Identificador Asigna Generador_de_objetos Identificador Parentesis_Izq ParametrosObjeto Parentesis_Der PuntoComa ;
ParametrosObjeto ::= /* vacío */ | IdNuLi MasParametrosObjeto ;
MasParametrosObjeto ::= /* vacío */ | Coma IdNuLi MasParametrosObjeto ;

// -------------- lista de instrucciones sencillas --------------
ListaInstSencillas ::= Instrucciones ListaInstSencillas | /* vacío */;
Instrucciones ::= InstVariable | InstPrint | InstArreglo | InstWhile | InstrAddArreglo | InstIf | InstFor | InstLlamarMetodo | InstInput | InstArbirArchivo;

//instruccion para declarar variable
InstVariable ::= Identificador Asigna IdNuLi PuntoComa | VariableVacia ;
VariableVacia ::= Identificador PuntoComa ;
IdNuLi ::= Identificador | Numero | Literal;

//instruccion para print
InstPrint ::= Imprime Parentesis_Izq ParametroPrint Parentesis_Der PuntoComa;
ParametroPrint ::= IdNuLi MasParametrosPrint ;
MasParametrosPrint ::= Coma ParametroPrint | /* vacío */;

//instruccion para declarar un arreglo con o sin elementos
InstArreglo ::= Tipo_de_dato Corchete_Izq Corchete_Der Identificador Asigna Corchete_Izq ParametroArreglo Corchete_Der;
ParametroArreglo ::= /* vacío */ | IdNuLi MasParametrosArreglo ;
MasParametrosArreglo ::= Coma ParametroArreglo | /* vacío */ ;

//instruccion para ciclo while
InstWhile ::= Ciclo_Mientras Condicion Llave_Izq ListaInstSencillas Llave_Der;
Condicion ::= Identificador Operador_comparativo NuId;
NuId ::= Numero | Identificador ;

//instruccion para agregar 1 o mas elementos a un arreglo
InstrAddArreglo ::= Identificador Punto Agregador Parentesis_Izq ParametroPrint Parentesis_Der PuntoComa;

//instruccion para if
InstIf ::= Si Parentesis_Izq Condicion Parentesis_Der Llave_Izq ListaInstSencillas Llave_Der IfSino SinoIf;
IfSino ::= Sino_pregunta Parentesis_Izq Condicion Parentesis_Der Llave_Izq ListaInstSencillas Llave_Der IfSino | /* vacío */ ;
SinoIf ::= Sino Llave_Izq ListaInstSencillas Llave_Der ;

//instruccion para ciclo for
InstFor ::= Ciclo_Para Parentesis_Izq Condicion Coma SubeBaja Parentesis_Der Llave_Izq ListaInstSencillas Llave_Der;
SubeBaja ::= Identificador IncDec ;
IncDec ::= Incremento | Decremento ;

//instruccion para llamar a un metodo
InstLlamarMetodo ::= Identificador Parentesis_Izq ParametroArreglo Parentesis_Der PuntoComa ;

//instruccion de input
InstInput ::= Ingresa Parentesis_Izq Parentesis_Der PuntoComa ;

//instruccion para abrir un archivo
InstArbirArchivo ::= Abridor_de_archivos Parentesis_Izq Literal Parentesis_Der PuntoComa ;